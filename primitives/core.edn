{:token :abort}

{:token :abs}

;; ACCEPT [CORE] 6.1.0695 ( c-addr +n1 -- +n2 )
;;
;; Receive a string of at most +n1 characters.  An ambiguous condition
;; exists if +n1 is zero or greater than 32,767.  Display graphic
;; characters as they are received.  A program that depends on the
;; presence or absence of non-graphic characters in the string has an
;; environmental dependency.  The editing functions, if any, that the
;; system performs in order to construct the string are
;; implementation-defined.
;;
;; Input terminates when an implementation-defined line terminator is
;; received.  When input terminates, nothing is appended to the string,
;; and the display is maintained in an implementation-defined way.
;;
;; +n2 is the length of the string stored at c-addr.
{:token :accept
 :args [[:c-addr :n1] [:n2]]
 :source ": ACCEPT ( c-addr max -- n)
            OVER + OVER ( ca-start ca-end ca-dest)
            BEGIN  KEY  DUP 10 <> WHILE
              DUP 8 =  OVER 127 =  OR IF ( cas cae cad bs)
                2OVER DROP NIP ( cas cae cad cas) OVER <> IF
                  ( cas cae cad) 1-  8 EMIT  SPACE  8 EMIT
                THEN
              ELSE
                DUP 2OVER ( cas cae cad c c cae cad) <> IF
                  EMIT OVER C! ( cas cae cad) 1+
                ELSE
                  ( cas cae cad c c) 2DROP
                THEN
              THEN
            REPEAT
            ( ca-start ca-end ca-dest c) DROP NIP SWAP - ;"
 :pfa [:over :plus :over
       :key :dup :icharlit 10 :notequals :izbranch 43
         :dup :icharlit 8 :equals :over :icharlit 127 :equals :or :izbranch 18
           :twoover :drop :nip :over :notequals :izbranch 9
             :oneminus :icharlit 8 :emit :space :icharlit 8 :emit
             :ibranch 13
         :dup :twoover :notequals :izbranch 7
           :emit :over :cstore :oneplus :ibranch 2
           :twodrop
         :ibranch -48
       :drop :nip :swap :minus
       :exit]}

{:token :again
 :args [[] []]
 :flags #{:immediate}
 :source ": AGAIN ( C: dest -- )
            ['] branch COMPILE,  HERE - C, ; IMMEDIATE"
 :pfa [:icharlit :branch :compilecomma :here :minus :ccomma :exit]}

{:token :align
 :args [[] []]
 :source ": ALIGN ( -- )  ( do nothing ) ;"
 :pfa [:exit]}

{:token :aligned
 :args [[:addr] [:a-addr]]
 :source ": ALIGNED ( addr -- a-addr )  ( do nothing ) ;"
 :pfa [:exit]}

{:token :allot
 :args [[:n] []]
 :source ": ALLOT ( n -- )  DP +! ;"
 :pfa [:dp :plusstore :exit]}

{:token :and}

{:token :base
 :args [[] [:aaddr]]
 :pfa [:vm :icharlit "offsetof(EnforthVM, base)" :plus :exit]}

{:token :begin
 :args [[] []]
 :flags #{:immediate}
 :source ": BEGIN ( -- )  HERE ; IMMEDIATE"
 :pfa [:here :exit]}

{:token :bl
 :args [[] [:c]]
 :source ": BL ( --)  $20 ;"
 :pfa [:icharlit "' '" :exit]}

{:token :bracketchar
 :name "[CHAR]"
 :args [[] [:char]]
 :flags #{:immediate}
 :source ": [CHAR] ( \"<spaces>name\" -- char )
            CHAR  ['] CHARLIT COMPILE,  C, ; IMMEDIATE"
 :pfa [:char :icharlit :charlit :compilecomma :ccomma :exit]}

{:token :brackettick
 :name "[']"
 :args [[] []]
 :flags #{:immediate}
 :source ": ['] ( \"<spaces>name\" -- )  '  ['] LIT COMPILE,  , ; IMMEDIATE"
 ; We keep things simple and use LIT here even if the XT is a token (and
 ; could therefore use CHARLIT).  ['] is used infrequently and
 ; optimizing for RAM size at the expense of the extra IF/ELSE statement
 ; here doesn't seem worth the complexity.
 :pfa [:tick :icharlit :lit :compilecomma :comma :exit]}

{:token :ccomma
 :name "C,"
 :args [[:char] []]
 :source ": C, ( char -- )  HERE C!  1 CHARS ALLOT ;"
 :pfa [:here :cstore :icharlit 1 :chars :allot :exit]}

{:token :cellplus
 :name "CELL+"
 :args [[:a-addr1] [:a-addr2]]
 :source ": CELL+ ( a-addr1 -- a-addr2 )  kEnforthCellSize + ;"
 :pfa [:icharlit "kEnforthCellSize" :plus :exit]}

{:token :cells
 :args [[:n1] [:n2]]
 :source ": CELLS ( n1 -- n2 )  kEnforthCellSize * ;"
 :pfa [:icharlit "kEnforthCellSize" :star :exit]}

{:token :cfetch :name "C@"}

{:token :char
 :args [[] [:char]]
 :source ": CHAR ( \"<spaces>name\" -- char )  BL PARSE-WORD DROP C@ ;"
 :pfa [:bl :parseword :drop :cfetch :exit]}

{:token :charplus
 :name "CHAR+"
 :args [[:c-addr1] [:c-addr2]]
 :source ": CHAR+ ( c-addr1 -- c-addr2 )  1+ ;"
 :pfa [:oneplus :exit]}

{:token :chars
 :args [[:n1] [:n2]]
 :source ": CHARS ( n1 -- n2 )  ( do nothing );"
 :pfa [:exit]}

;; : [CORE] 6.1.0450 "colon" ( C: "<spaces>name" -- colon-sys )
;;
;; Skip leading space delimiters.  Parse name delimited by a space.
;; Create a definition for name, called a "colon definition".  Enter
;; compilation state and start the current definition, producing
;; colon-sys.  Append the initiation semantics given below to the
;; current definition.
;;
;; The execution semantics of name will be determined by the words
;; compiled into the body of the definition.  The current definition
;; shall not be findable in the dictionary until it is ended (or until
;; the execution of DOES> in some systems).
;;
;; Initiation: ( i*x -- i*x ) ( R: -- nest-sys )
;;   Save implementation-dependent information nest-sys about the
;;   calling definition.  The stack effects i*x represent arguments to
;;   name.
;;
;; name Execution: ( i*x -- j*x )
;;   Execute the definition name.  The stack effects i*x and j*x
;;   represent arguments to and results from name, respectively.
{:token :colon
 :name ":"
 :args [[] []]
 :source ": : ( \"<spaces>name\" -- )
            CREATE  HIDE  -1 ALLOT ['] DOCOLON C,  ]"
 :pfa [:create :hide
       :zero :oneminus :allot :icharlit "DOCOLON" :ccomma
       :rtbracket
       :exit]}

{:token :comma
 :name ","
 :args [[:x] []]
 :source ": , ( x -- )  HERE !  1 CELLS ALLOT ;"
 :pfa [:here :store :icharlit 1 :cells :allot :exit]}

{:token :compilecomma
 :name "COMPILE,"
 :args [[:xt] []]
 :source ": COMPILE, ( xt --)
            DUP TOKEN? IF C, EXIT THEN
            DUP ROMDEF? 0= IF XT, EXIT THEN
            DUP ROMXT>CFA IC@  DUP $70 < IF C, DROP ELSE DROP XT, THEN ;"
 :pfa [:dup :tokenq :izbranch 3 :ccomma :exit
       :dup :romdefq :zeroequals :izbranch 3 :xtcomma :exit
       :dup :romxttocfa :icfetch :dup :icharlit 0x70 :lessthan :izbranch 5
         :ccomma :drop :ibranch 3
         :drop :xtcomma
       :exit]}

{:token :constant
 :args [[:x] []]
 :source ": CONSTANT ( x \"<spaces>name\" -- )
            CREATE  -1 ALLOT ['] DOCONSTANT C,  , ;"
 :pfa [:create :zero :oneminus :allot :icharlit "DOCONSTANT" :ccomma
       :comma
       :exit]}

{:token :count
 :args [[:caddr1] [:caddr2 :u]]
 :source ": COUNT ( c-addr1 -- c-addr2 u )  DUP C@  SWAP 1+ SWAP ;"
 :pfa [:dup :cfetch :swap :oneplus :swap :exit]}

{:token :cstore :name "C!"}

{:token :cr
 :args [[] []]
 :source ": CR ( --)  $0A EMIT ;"
 :pfa [:icharlit 0x0a :emit :exit]}

{:token :create
 :args [[] []]
 :source ": NAME, ( ca u --)
            OVER + ( ca-start ca-end)
            1- DUP C@ $80 OR C, ( Write last char with end-of-name flag)
            BEGIN
              ( cas cae) 2DUP <>
            WHILE
              1- DUP C@ C,
            REPEAT ( cas cas) 2DROP ;
          : CREATE ( \"<spaces>name\" -- )
            BL PARSE-WORD DUP 0= IF ABORT THEN ( ca u)
            TUCK ( u ca u) NAME, ( u)
            HERE >XT ( u this-xt)  SWAP C,  LATEST @ XT,  ( this-xt) LATEST !
            ['] DOCREATE C,  ALIGN ;"
 :pfa [:bl :parseword :dup :zeroequals :izbranch 2 :abort
       :tuck
       ; NAME,
         :over :plus
         :oneminus :dup :cfetch :icharlit 0x80 :or :ccomma
         :twodup :notequals :izbranch 7
           :oneminus :dup :cfetch :ccomma :ibranch -9
         :twodrop
       :here :toxt :swap :ccomma :latest :fetch :xtcomma :latest :store
       :icharlit "DOCREATE" :ccomma
       :align
       :exit]}

{:token :decimal
 :args [[] []]
 :source ": DECIMAL ( -- )  10 BASE ! ;"
 :pfa [:icharlit 10 :base :store :exit]}

{:token :depth}

; do-sys in Enforth is ( do-orig ).  LEAVE locations chain from the most
; recent LEAVE to the oldest LEAVE and then to zero, which signifies the
; end of the LEAVE list.  LOOP/+LOOP go through the LEAVE list and fix
; up the addresses.
{:token :do
 :flags #{:immediate}
 :source ": DO ( C: -- do-sys )
            0 'PREVLEAVE !  ['] (DO) COMPILE,  HERE ; IMMEDIATE"
 :pfa [:zero :tickprevleave :store :icharlit :pdo :compilecomma :here :exit]}

{:token :dot
 :name "."
 :args [[:n] []]
 :source "BASE @ 10 <>  IF U. EXIT THEN
         DUP ABS 0 <# #S ROT SIGN #> TYPE SPACE ;"
 :pfa [:base :fetch :icharlit 10 :notequals :izbranch 3 :udot :exit
       :dup :abs :zero :lessnumsign :numsigns :rot :sign,
       :numsigngrtr :type :space
       :exit]}

{:token :drop :args [[:x1 :x2] [:x1]]}

{:token :dup :args [[:x] [:x :x]]}

{:token :else
 :flags #{:immediate}
 :source ": ELSE ( C: orig1 -- orig2 )
            ['] branch COMPILE,  HERE  0 C,  SWAP POSTPONE THEN ; IMMEDIATE"
 :pfa [:icharlit :branch :compilecomma :here :zero :ccomma :swap :then :exit]}

{:token :emit}

{:token :equals :name "="}

;; TODO Implement this properly; probably after we add MFORTH's Input
;; Control Blocks to Enforth..?
{:token :evaluate
 :args [[:caddr :u] []]
 :flags #{:headerless} ;; Because it isn't really useful/correct without ICBs.
 :source ": EVALUATE ( i*x c-addr u -- j*x )  INTERPRET ;"
 :pfa [:interpret :exit]}

{:token :exit
 :args [[] []]}

{:token :false}

{:token :fetch :name "@"}

;; FIND [CORE] 6.1.1550 ( c-addr -- c-addr 0 | xt 1 | xt -1 )
;;
;; Find the definition named in the counted string at c-addr.  If the
;; definition is not found, return c addr and zero.  If the definition
;; is found, return its execution token xt.  If the definition is
;; immediate, also return one (1), otherwise also return minus-one (-1).
;; For a given string, the values returned by FIND while compiling may
;; differ from those returned while not compiling.
{:token :find
 :args [[:caddr] [[:caddr 0] [:xt 1] [:xt -1]]]
 :source ": FIND ( c-addr -- c-addr 0 | xt 1 | xt -1 ) COUNT FIND-WORD ;"
 :pfa [:count :findword :exit]}

{:token :fmslashmod
 :name "FM/MOD"
 :args [[:d1 :n1] [:n2 :n3]]
 :source ": FM/MOD ( d1 n1 -- n2 n3)
            DUP >R ( num den R:signrem) 2DUP XOR ( num den signquo R:signrem)
            SWAP ABS DUP >R ( num signquo +den R:signrem +den)
            SWAP >R >R DABS R> ( num +den R:signrem +den signquo)
            UM/MOD ( rem quo R:..)
            R> 0< IF NEGATE OVER 0<> IF 1- SWAP R> SWAP - SWAP
                                     ELSE R> DROP THEN
                  ELSE R> DROP THEN
            R> 0< IF SWAP NEGATE SWAP THEN ;"
 :pfa [:dup :tor :twodup :xor
       :swap :abs :dup :tor
       :swap :tor :tor :dabs :rfrom
       :umslashmod
       :rfrom :zeroless :izbranch 18
         :negate :over :zeronotequals :izbranch 9
           :oneminus :swap :rfrom :swap :minus :swap :ibranch 7
           :rfrom :drop :ibranch 3
         :rfrom :drop
       :rfrom :zeroless :izbranch 4
         :swap :negate :swap
       :exit]}

;; EXECUTE [CORE] 6.1.1370 ( i*x xt -- j*x )
;;
;; Remove xt from the stack and perform the semantics identified by it.
;; Other stack effects are due to the word EXECUTEd.
{:token :execute
 :args [[:xt] []]}

{:token :greaterthan :name ">"}

{:token :here
 :args [[] [:addr]]
 :source ": HERE ( -- addr ) DP @ ;"
 :pfa [:dp :fetch :exit]}

;; HOLD [CORE] 6.1.1670 ( char -- )
;;
;; Add char to the beginning of the pictured numeric output string.  An
;; ambiguous condition exists if HOLD executes outside of a <# #>
;; delimited number conversion.
{:token :hold
 :args [[:c] []]
 :source ": HOLD ( c -- )   HLD @ 1- DUP HLD ! C! ;"
 :pfa [:hld :fetch :oneminus :dup :hld :store :cstore :exit]}

{:token :i
 :args [[] [:n]]}

{:token :if
 :flags #{:immediate}
 :source ": IF ( C: -- orig )  ['] 0branch COMPILE,  HERE  0 C, ; IMMEDIATE"
 :pfa [:icharlit :zbranch :compilecomma :here :zero :ccomma :exit]}

{:token :immediate
 :args [[] []]
 :source ": IMMEDIATE ( --)
            LATEST @ >NFA
            ( addr) DUP C@ %10000000 OR ( addr val)
            ( addr val) SWAP C!"
 :pfa [:latest :fetch :tonfa
       :dup :cfetch :icharlit 0x80 :or
       :swap :cstore
       :exit]}

{:token :invert}

{:token :key}

{:token :leave
 :args [[] []]
 :flags #{:immediate}
 :source ": LEAVE ( do-orig)
            ['] UNLOOP COMPILE,  ['] branch COMPILE,
            HERE  'PREVLEAVE @ DUP IF HERE SWAP - THEN C,
            'PREVLEAVE ! ; IMMEDIATE"
 :pfa [:icharlit :unloop :compilecomma :icharlit :branch :compilecomma
       :here :tickprevleave :fetch :dup :izbranch 4
         :here :swap :minus
       :ccomma
       :tickprevleave :store
       :exit]}

;; <# [CORE] 6.1.0490 "less-number-sign" ( -- )
;;
;; Initialize the pictured numeric output conversion process.
;
; ---
; : <# ( -- )   HERE HLDEND + HLD ! ;
{:token :lessnumsign
 :name "<#"
 :args [[] []]
 :source ": <# ( -- )   HERE HLDEND + HLD ! ;"
 :pfa [:here :hldend :plus :hld :store :exit]}

{:token :lessthan :name "<"}

{:token :literal
 :args [[:x] []]
 :flags #{:immediate}
 :source ": LITERAL ( x --)
            DUP $FF INVERT AND 0= IF CHARLIT C, C,
            ELSE LIT C, , THEN ; IMMEDIATE"
 :pfa [:dup :icharlit 0xff :invert :and :zeroequals :izbranch 7
         :icharlit :charlit :ccomma :ccomma :ibranch 5
         :icharlit :lit :ccomma :comma
       :exit]}

{:token :lshift :args [[:x1 :u] [:x2]]}

{:token :loop
 :args [[] []]
 :flags #{:immediate}
 :source ": LOOP ( C: do-sys -- )  ['] (LOOP) END-LOOP ; IMMEDIATE"
 :pfa [:icharlit :ploop :endloop :exit]}

{:token :ltbracket
 :name "["
 :args [[] []]
 :flags #{:immediate}
 :source ": [ ( --)  FALSE STATE ! ; IMMEDIATE"
 :pfa [:false :state :store :exit]}

{:token :max :args [[:n1 :n2] [:n3]]}

{:token :min :args [[:n1 :n2] [:n3]]}

{:token :minus :name "-"}

{:token :mod
 :args [[:n1 :n2] [:n3]]
 :source ": MOD ( n1 n2 -- n3 )  /MOD DROP ;"
 :pfa [:slashmod :drop :exit]}

{:token :move}

{:token :mplus :name "M+"}

{:token :mstar
 :name "M*"
 :args [[:n1 :n2] [:d]]
 :source ": M* ( n1 n2 -- d )   2DUP XOR 0< >R  ABS SWAP ABS UM*  R> ?DNEGATE ;"
 :pfa [:twodup :xor :zeroless :tor
       :abs :swap :abs :umstar
       :rfrom :qdnegate
       :exit]}

{:token :nip}

{:token :negate}

{:token :notequals :name "<>"}

;; # [CORE] 6.1.0030 "number-sign" ( ud1 -- ud2 )
;;
;; Divide ud1 by the number in BASE giving the quotient ud2 and the
;; remainder n.  (n is the least-significant digit of ud1.) Convert n
;; to external form and add the resulting character to the beginning
;; of the pictured numeric output string.  An ambiguous condition
;; exists if # executes outside of a <# #> delimited number
;; conversion.
{:token :numsign
 :name "#"
 :source ": >DIGIT ( u -- c ) DUP 9 > 7 AND + 48 + ;
          : # ( ud1 -- ud2 )   BASE @ UD/MOD ROT >digit HOLD ;"
 :pfa [:base :fetch :udslashmod :rot
       ; TODIGIT
         :dup :icharlit 9 :greaterthan :icharlit 7 :and
         :plus :icharlit 48 :plus
       :hold
       :exit]}

;; #> [CORE] 6.1.0040 "number-sign-greater" ( xd -- c-addr u )
;;
;; Drop xd.  Make the pictured numeric output string available as a
;; character string.  c-addr and u specify the resulting character
;; string.  A program may replace characters within the string.
{:token :numsigngrtr
 :name "#>"
 :args [[:xd] [:caddr :u]]
 :source "#> ( xd -- c-addr u ) DROP DROP  HLD @  HERE HLDEND +  OVER - ;"
 :pfa [:drop :drop :hld :fetch :here :hldend :plus :over :minus :exit]}

;; #S [CORE] 6.1.0050 "number-sign-s" ( ud1 -- ud2 )
;;
;; Convert one digit of ud1 according to the rule for #.  Continue
;; conversion until the quotient is zero.  ud2 is zero.  An ambiguous
;; condition exists if #S executes outside of a <# #> delimited number
;; conversion.
{:token :numsigns
 :name "#S"
 :args [[:ud1] [:ud2]]
 :source ": #S ( ud1 -- 0 )   BEGIN # 2DUP OR WHILE REPEAT ;"
 :pfa [:numsign :twodup :or :izbranch 3 :ibranch -6 :exit]}

{:token :oneplus :name "1+"}

{:token :oneminus :name "1-"}

{:token :or}

{:token :over}

{:token :paren
 :name "("
 :args [[] []]
 :flags #{:immediate}
 :source ": ( ( \"ccc<quote>\" -- )  [CHAR] ) PARSE-WORD 2DROP ; IMMEDIATE"
 :pfa [:icharlit "')'" :parseword :twodrop :exit]}

{:token :plus :name "+"}

{:token :plusloop
 :name "+LOOP"
 :args [[:n] []]
 :flags #{:immediate}
 :source ": +LOOP ( n -- ; C: do-sys -- )  ['] (+LOOP) END-LOOP ; IMMEDIATE"
 :pfa [:icharlit :pplusloop :endloop :exit]}

{:token :plusstore :name "+!"}

; Postponing a non-immediate word requires the compiler to add code to
; the current definition that compiles the postponed word into the
; then-current definition when the current definition is executed.  In
; other words, the current definition is probably a compiler directive
; and we want the compilation behavior of the postponed word in that
; compiler directive and not the execution behavior.
;
; The corollary to this is postponing an immediate word, which requires
; the word to be compiled into the current definition (so that it
; executes when that definition executes) instead of being executed
; right now, which is the usual behavior of an immediate word.
{:token :postpone
 :args [[] []]
 :flags #{:immediate}
 :source ": POSTPONE ( \"<spaces>name\" --)
            BL PARSE-WORD  FIND-WORD  ( 0 | xt 1 [imm] | xt -1 [non-imm])
            ?DUP 0= IF TYPE  SPACE  [CHAR] ? EMIT  CR  ABORT THEN
            0< IF  ['] LIT COMPILE,  ,  ['] COMPILE, COMPILE,
               ELSE COMPILE, THEN ; IMMEDIATE"
 :pfa [:bl :parseword :findword
       :qdup :zeroequals :izbranch 8
         :type :space :icharlit "'?'" :emit :cr :abort
       :zeroless :izbranch 16
         ; We keep things simple and use LIT here even if the XT is a
         ; token (and could therefore use CHARLIT).  POSTPONE is used
         ; infrequently and optimizing for RAM size at the expense of
         ; the extra IF/ELSE statement here doesn't seem worth the
         ; complexity.
         :icharlit :lit :compilecomma
           :comma
           :icharlit "((uint8_t)((ROMDEF_COMPILECOMMA >> 8) & 0xff))"
           :icharlit 8 :lshift
           :icharlit "((uint8_t)((ROMDEF_COMPILECOMMA     ) & 0xff))"
           :or
           :xtcomma
           :ibranch 2
         :compilecomma
       :exit]}

{:token :qdo
 :name "?DO"
 :flags #{:immediate}
 :source ": ?DO ( C: -- do-sys )
            ['] (?DO) COMPILE,  HERE 'PREVLEAVE !  0 C,  HERE ; IMMEDIATE"
 :pfa [:icharlit :pqdo :compilecomma
       :here :tickprevleave :store
       :zero :ccomma
       :here
       :exit]}

{:token :qdup :name "?DUP"}

;; QUIT [CORE] 6.1.2050 ( -- ) ( R:  i*x -- )
;;
;; Empty the return stack, store zero in SOURCE-ID if it is
;; present, make the user input device the input source, and
;; enter interpretation state.  Do not display a message.
;; Repeat the following:
;;   - Accept a line from the input source into the input
;;     buffer, set >IN to zero, and interpret.
;;   - Display the implementation-defined system prompt if in
;;     interpretation state, all processing has been completed,
;;     and no ambiguous condition exists.
{:token :quit
 :args [[] []]
 :source ": QUIT    --; R: i*x --)
            INITRP  0 STATE !
            BEGIN
                TIB  DUP TIBSIZE ACCEPT  SPACE
                INTERPRET
                CR  STATE @ 0= IF .\" ok \" THEN
            AGAIN ;"
 :pfa [:initrp :zero :state :store
         :tib :dup :tibsize :accept :space
         :interpret
         :cr :state :fetch :zeroequals :izbranch 7
         :pisquote 3 "'o'" "'k'" "' '" :itype
         :ibranch -19]}

{:token :repeat
 :args [[] []]
 :flags #{:immediate}
 :source ": REPEAT ( -- )  POSTPONE AGAIN  POSTPONE THEN ; IMMEDIATE"
 :pfa [:again :then :exit]}

{:token :rfetch :name "R@"}

{:token :rfrom :name "R>"}

{:token :rshift :args [[:x1 :u] [:x2]]}

{:token :rtbracket
 :name "]"
 :args [[] []]
 :source ": ] ( --)  TRUE STATE ! ;"
 :pfa [:true :state :store :exit]}

{:token :rot}

;; ; [CORE] 6.1.0460 semicolon
;;
;; Interpretation:
;;   Interpretation semantics for this word are undefined.
;;
;; Compilation: ( C: colon-sys -- )
;;   Append the run-time semantics below to the current definition.
;;   End the current definition, allow it to be found in the dictionary
;;   and enter interpretation state, consuming colon-sys.  If the
;;   data-space pointer is not aligned, reserve enough data space to
;;   align it.
;;
;; Run-time: ( -- ) ( R: nest-sys -- )
;;   Return to the calling definition specified by nest-sys.
{:token :semicolon
 :name ";"
 :args [[] []]
 :flags #{:immediate}
 :source ": ; ( --)
            ['] EXIT COMPILE,
            REVEAL
            [ ; IMMEDIATE"
 :pfa [:icharlit :exit :compilecomma
       :reveal
       :ltbracket :exit]}

;; SIGN [CORE] 6.1.2210 ( n -- )
;;
;; If n is negative, add a minus sign to the beginning of the pictured
;; numeric output string.  An ambiguous condition exists if SIGN
;; executes outside of a <# #> delimited number conversion.
;;
;; ---
;; : SIGN ( n -- )   0< IF [CHAR] - HOLD THEN ;
;; ZEROLESS, ZBRANCH, 4, CHARLIT, '-', HOLD, EXIT, 0,
{:token :sign
 :args [[:n] []]
 :source ": SIGN ( n -- )   0< IF [CHAR] - HOLD THEN ;"
 :pfa [:zeroless :izbranch 4 :icharlit "'-'" :hold :exit]}

{:token :slash
 :name "/"
 :args [[:n1 :n2] [:n3]]
 :source ": / ( n1 n2 -- n3 )  /MOD NIP ;"
 :pfa [:slashmod :nip :exit]}

{:token :slashmod
 :name "/MOD"
 :args [[:n1 :n2] [:n2 :n4]]
 :source ": /MOD ( n1 n2 -- n3 n4)   >R S>D R> SM/REM ;"
 :pfa [:tor :stod :rfrom :smslashrem :exit]}

{:token :smslashrem
 :name "SM/REM"
 :args [[:d1 :n2] [:n2 :n3]]
 :source ": SM/REM ( d1 n1 -- n2 n3)
            OVER >R  2DUP XOR >R  ( R:remsign quosign)
            ABS >R DABS R>
            UM/MOD ( +rem +quo)
            R> ?NEGATE ( +rem +-quo)  SWAP R> ?NEGATE SWAP ( +-rem +-quo) ;"
 :pfa [:over :tor :twodup :xor :tor
       :abs :tor :dabs :rfrom
       :umslashmod
       :rfrom :qnegate :swap :rfrom :qnegate :swap
       :exit]}

{:token :source
 :args [[] [:caddr :u]]
 :pfa [:vm :icharlit "offsetof(EnforthVM, source_len)" :plus :twofetch :exit]}

{:token :space
 :args [[] []]
 :source ": SPACE ( --)  BL EMIT ;"
 :pfa [:bl :emit :exit]}

{:token :spaces
 :args [[:n] []]
 :source ": SPACES ( n -- )  BEGIN ?DUP WHILE SPACE 1- REPEAT ;"
 :pfa [:qdup :izbranch 5 :space :oneminus :ibranch -6 :exit]}

{:token :squote
 :name "S\""
 :args [[] []]
 :flags #{:immediate}
 :source ": S\" ( -- )
           [CHAR] \" PARSE-WORD
           ['] (S\") COMPILE,  ( ca u) DUP C,  HERE OVER ALLOT  SWAP MOVE ;
           IMMEDIATE"
 :pfa [:icharlit "'\"'" :parseword
       :icharlit :psquote :compilecomma
       :dup :ccomma :here :over :allot :swap :move
       :exit]}

{:token :star
 :name "*"
 :args [[:n1 :n2] [:n3]]
 :source ": * ( n1|u1 n2|u2 -- n3|u3 )  UM* DROP ;"
 :pfa [:umstar :drop :exit]}

{:token :starslash
 :name "*/"
 :args [[:n1 :n2 :n3] [:n4]]
 :source ": */ ( n1 n2 n3 -- n4)   */MOD NIP ;"
 :pfa [:starslashmod :nip :exit]}

{:token :starslashmod
 :name "*/MOD"
 :args [[:n1 :n2 :n3] [:n4 :n5]]
 :source ": */MOD ( n1 n2 n3 -- n4 n5)   >R M* R> SM/REM ;"
 :pfa [:tor :mstar :rfrom :smslashrem :exit]}

{:token :state
 :args [[] [:aaddr]]
 :pfa [:vm :icharlit "offsetof(EnforthVM, state)" :plus :exit]}

{:token :stod
 :name "S>D"
 :args [[:n] [:d]]
 :source ": S>D ( n -- d)   DUP 0< ; "
 :pfa [:dup :zeroless :exit]}

{:token :store :name "!"}

{:token :swap}

{:token :then
 :flags #{:immediate}
 :source ": THEN ( C: orig -- )  HERE OVER -  SWAP C! ; IMMEDIATE"
 :pfa [:here :over :minus :swap :cstore :exit]}

{:token :tick
 :name "'"
 :args [[] [:xt]]
 :source ": ' ( \"<spaces>name\" -- xt)
            BL PARSE-WORD  FIND-WORD
            0= IF TYPE  SPACE  [CHAR] ? EMIT  CR  ABORT THEN ;"
 :pfa [:bl :parseword :findword
       :zeroequals :izbranch 8
         :type :space :icharlit "'?'" :emit :cr :abort
       :exit]}

{:token :tobody
 :name ">BODY"
 :args [[:xt] [:a-addr]]
 :source ": >BODY ( xt -- a-addr)
            DUP ROMDEF? IF ROMXT>CFA 1+ EXIT THEN
            >CFA 1+ ;"
 :pfa [:dup :romdefq :izbranch 4 :romxttocfa :oneplus :exit
       :tocfa :oneplus
       ;; TODO Add :align if we start aligning the PFA.
       :exit]}

{:token :toin
 :name ">IN"
 :args [[] [:aaddr]]
 :pfa [:vm :icharlit "offsetof(EnforthVM, to_in)" :plus :exit]}

;; >NUMBER [CORE] 6.1.0567 "to-number" ( ud1 c-addr1 u1 -- ud2 c-addr2 u2 )
;;
;; ud2 is the unsigned result of converting the characters
;; within the string specified by c-addr1 u1 into digits, using
;; the number in BASE, and adding each into ud1 after
;; multiplying ud1 by the number in BASE.  Conversion continues
;; left-to-right until a character that is not convertible,
;; including any "+" or "-", is encountered or the string is
;; entirely converted.  c-addr2 is the location of the first
;; unconverted character or the first character past the end of
;; the string if the string was entirely converted.  u2 is the
;; number of unconverted characters in the string.  An ambiguous
;; condition exists if ud2 overflows during the conversion.
{:token :tonumber
 :name ">NUMBER"
 :args [[:ud1 :caddr1 :u1] [:ud2 :caddr2 :u2]]
 :source ": UD* ( ud1 u1 -- ud2)   DUP >R UM* DROP  SWAP R> UM* ROT + ;
          : >NUMBER ( ud1 c-addr1 u1 -- ud2 c-addr2 u2)
            BEGIN DUP WHILE
               OVER C@ DIGIT?  0= IF EXIT THEN
               >R 2SWAP BASE @ UD* R> M+ 2SWAP
               1 /STRING
            REPEAT ;"
 :pfa [:dup :izbranch 29
         :over :cfetch :digitq :zeroequals :izbranch 2 :exit
         :tor :twoswap :base :fetch
         ; UD*
           :dup :tor :umstar :drop :swap :rfrom :umstar :rot :plus
         :rfrom :mplus :twoswap
         :icharlit 1 :slashstring
         :ibranch -30
       :exit]}

{:token :tor :name ">R"}

{:token :true}

{:token :tuck}

{:token :twodrop :name "2DROP"}

{:token :twodup :name "2DUP"}

{:token :twofetch :name "2@"}

{:token :twoslash
 :name "2/"
 :args [[:x1] [:x2]]}

{:token :twostar
 :name "2*"
 :args [[:x1] [:x2]]}

{:token :twostore :name "2!"}

{:token :twoswap :name "2SWAP"}

{:token :twoover :name "2OVER"}

{:token :type
 :args [[:caddr :u] []]
 :source ": TYPE ( c-addr u --)
            OVER + SWAP  ( ca-end ca-next)
            BEGIN 2DUP <> WHILE DUP C@ EMIT 1+ REPEAT 2DROP ;"
 :pfa [:over :plus :swap
       :twodup :notequals :izbranch 7
         :dup :cfetch :emit :oneplus :ibranch -9
       :twodrop
       :exit]}

;; U. [CORE] 6.1.2320 u-dot ( u -- )
;;
;; Display u in free field format.
{:token :udot
 :name "U."
 :args [[:u] []]
 :source ": U. ( u --)  0 <# #S #> TYPE SPACE ;"
 :pfa [:zero :lessnumsign :numsigns :numsigngrtr :type :space :exit]}

{:token :ugreaterthan :name "U>"}

{:token :ulessthan :name "U<"}

{:token :umslashmod
 :name "UM/MOD"
 :args [[:ud :u1] [:u2 :u3]]}

{:token :umstar :name "UM*"}

{:token :unloop
 :args [[] []]}

{:token :variable
 :args [[] []]
 :source ": VARIABLE ( \"<spaces>name\" -- )
            CREATE  -1 ALLOT ['] DOVARIABLE C,  0 , ;"
 :pfa [:create :zero :oneminus :allot :icharlit "DOVARIABLE" :ccomma
       :zero :comma
       :exit]}

{:token :while
 :args [[] []]
 :flags #{:immediate}
 :source ": WHILE ( -- )  POSTPONE IF  SWAP ; IMMEDIATE"
 :pfa [:if :swap :exit]}

{:token :xor}

{:token :zeroequals :name "0="}

{:token :zeroless :name "0<"}
